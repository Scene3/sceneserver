/---------------------
 -- sceneserver_mgr.bento
 --
 -- The scene manager module for sceneserver.
 --
 -- Copyright (c) 2013-2014 by Michael St. Hippolyte
 --
 --/
 
site sceneserver [=

    adopt three

    /** Global world model.  All primary scenes are retrieved from this model. **/ 
    global bento_domain world_domain(bento_domain w) = w

    /** Global cache for external world models.  External scenes may be retrieved from these 
     *  models and injected into the primary scene.
     **/
    global bento_domain{} external_domains = {}
    

    dynamic load_world(name, path) [=
        bento_domain load_world_domain = this_domain.child_domain(name, "world", path, "*.world", true)  

        eval(world_domain(: load_world_domain :));    
    =]
    
    scene retrieve_scene(name) [=
        scene instantiate_scene = world_domain.context.construct(name) 

        instantiate_scene;        
    =]
    

    dynamic path_to_name(p) [=
        log("get name for path `" + p + "`");
        if (!p) [=
            "home";
        =] else if (index_of(p, " ") >= 0 ||
                    index_of(p, ".") >= 0 ||
                    index_of(p, "/") >= 0) [=
            "error";
        =] else [=
            p;
        =]
    =]    
    
    dynamic scene get_scene_for_path(path) [=
        scene_name = path_to_name(path)
    
        if (!current_scene || current_scene.name != scene_name) [=
            current_scene(: retrieve_scene(scene_name) :);
        =] else [=
            current_scene;
        =]
    =]
    
=]
