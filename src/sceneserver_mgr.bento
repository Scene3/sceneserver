/---------------------
 -- sceneserver_mgr.bento
 --
 -- The scene manager module for sceneserver.
 --
 -- Copyright (c) 2013-2014 by Michael St. Hippolyte
 --
 --/
 
site sceneserver [=

    adopt three

    global scene{} scene_cache = {}
    
    keep by name in scene_cache: 
    scene generated_scene(code) [=
        
        name = create_scene.name
        
        scene create_scene [[
            code;
        ]]

        create_scene;
    =]
    

    dynamic path_to_name(p) [=
        log("get name for path `" + p + "`");
        if (!p) [=
            "home";
        =] else if (index_of(p, " ") >= 0 ||
                    index_of(p, ".") >= 0 ||
                    index_of(p, "/") >= 0) [=
            "error";
        =] else [=
            p;
        =]
    =]    
    
    dynamic scene get_scene_for_path(path) [=
        scene_name = path_to_name(path)
    
        if (!scene_cache{scene_name}) [=
            load_scene(scene_name);
        =]

        scene_cache{scene_name};
    =]
    
    dynamic file_name_for_scene(scene_name) = file_base + this_config.world_path + scene_name + ".scene"
    
    dynamic load_scene(scene_name) [=
        
        scene_code = include_file(file_name_for_scene(scene_name))

        log("loading scene " + scene_name + " from file");
        log("world path: " + file_base + this_config.world_path);
        
        
        eval(generated_scene(scene_code));
        if (scene_cache{scene_name}) [=
            log("scene loaded: " + scene_cache{scene_name}.name);
        =] else [=
            log("unable to load scene");
        =]
    =]    
=]
